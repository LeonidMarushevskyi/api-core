plugins {
    id "org.sonarqube" version "2.2.1"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    sourceCompatibility = 1.8
    compileJava {options.encoding = "UTF-8"}

    project.ext {
        apiVersion = "3.6"
        commonGroupId = "gov.ca.cwds.api"
        shiroSecurityVersion = "0.25.69"
        configPath = "$rootProject.projectDir/config/"
        dropwizardVersion = "1.1.0"
        dropwizardFlywayVersion = "1.0.0-1"
        metricsVersion = "3.1.0"
        swaggerVersion = "1.5.9"
        dropwizardSwaggerVersion = "1.0.0-1"
        elasticsearchVersion = "5.5.0"
        log4jVersion = "2.8.2"
        logbackVersion = "1.2.1"
        slf4jVersion = "1.7.25"
        mockitoVersion = "2.0.54-beta"
        hamcrestVersion = "2.0.0.0"
        apacheCommonsVersion = "1.3.2"
        commonsBeanutilsVersion = "1.8.3"
        guavaVersion = "22.0"
        glassfishJerseyCoreVersion = "2.25.1"
        glassfishJerseyExtVersion = "2.23.2"
        jacksonVersion = "2.8.2"
        postgresqlVersion = "9.4.1208.jre7"
    }

    version = apiVersion

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://maven2.cwds.tabordasolutions.net"
        }
    }

    dependencies {
        compile group: 'org.apache.commons', name: 'commons-lang3', version: apacheCommonsVersion
        compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-guice', version: jacksonVersion
        compile group: 'org.apache.commons', name: 'commons-io', version: apacheCommonsVersion
        compile group: 'commons-beanutils', name: 'commons-beanutils', version: commonsBeanutilsVersion


        // LOGGING:
        compile group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
        compile group: 'org.slf4j', name: 'slf4j-ext', version: slf4jVersion
        compile group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion

        // TEST:
        testCompile group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
        testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: hamcrestVersion
    }

    tasks.withType(Test) {
        enableAssertions = true
    }

    test {
        exclude '**/*IT*'
    }

    task integrationTest(type: Test, dependsOn: testClasses) {
        include '**/*IT*'
        exclude '**/*Test*'
    }

    javadoc {
        options.addStringOption("charset", "UTF-8")
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            csv.enabled false
            html.destination "${buildDir}/jacocoHtml"
        }
    }

    sonarqube {
        properties {
            property "sonar.jacoco.reportPath", "${buildDir}/jacoco/test.exec"
            property "sonar.jacoco.itReportPath", "${buildDir}/jacoco/integrationTest.exec"
        }
    }

}

dependencies {
    compile project(':api-core-rest')
    compile project(':api-core-cms')
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
            groupId commonGroupId
            artifactId 'api-core'
            version version + ((System.properties.getProperty('build') != null ) ? '.' + System.properties.getProperty('build') : '')
        }
    }
    repositories {
        maven {
            //TODO : set to http url
            url "file:///usr/share/nginx/html/maven2"
        }
    }
}

