import gov.ca.cwds.rest.exception.IssueDetails
import gov.ca.cwds.data.legacy.cms.entity.Client
import gov.ca.cwds.cms.data.access.dto.ClientEntityAwareDTO;
import gov.ca.cwds.data.legacy.cms.entity.MedicalEligibilityApplication

global java.util.Set<IssueDetails> validationDetailsList

rule "R-02127"
  dialect "mvel"
  agenda-group "client-agenda"
  when
      /*
        Code: R - 02127

        Rule Txt:
        Adoption Agreement Term Date cannot be earlier than birth date. If the Adoption Agreement Term Date is entered,
        then the Client's birth date cannot be changed to be greater than that of the Adoption Agreement Term Date.

        Logic:
        MEDICAL_ELIGIBILITY_APPLICATION.Adoption_Agreement_Term_Date >= CHILD_CLIENT>CLIENT.Birth_Date.
      */
      $clientAwareDTO: ClientEntityAwareDTO()
      $client: Client() from $clientAwareDTO.entity
      $medicalEligibilityApplication : MedicalEligibilityApplication() from $clientAwareDTO.medicalEligibilityApplications
      Boolean(booleanValue == true) from $medicalEligibilityApplication.adoptionAgreementTermDate != null && $client.birthDate != null
                                                && $medicalEligibilityApplication.adoptionAgreementTermDate.isBefore($client.birthDate)

  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-02127");
      details.setUserMessage("Adoption Agreement Term Date cannot be earlier than birth date");
      details.setTechnicalMessage("If MedicalEligibilityApplication.adoptionAgreementTermDate != null then should be: MedicalEligibilityApplication.adoptionAgreementTermDate >= Client.birthDate");
      validationDetailsList.add(details);
end
