import gov.ca.cwds.rest.exception.IssueDetails
import gov.ca.cwds.data.legacy.cms.entity.PlacementHome
import gov.ca.cwds.cms.data.access.dto.PlacementHomeEntityAwareDTO;

global java.util.Set validationDetailsList

rule "R-03960-CT"
  dialect "mvel"
  agenda-group "placement-home-data-processing-agenda"
  when
      /*
      Code: R - 00195

      Fields: fraLicenser

      Rule Txt

      On add, if the placement facility type is Resource Family Home or Foster Family Home, default Licensed By to 'County', otherwise use 'Not Applicable'.
      Logic

      If PLACEMENT_HOME.Placement_Facility_Type = 'Resource Family Home' or 'Foster Family Home', set .Licenser_Code = 'CT', else set .Licenser_Code = 'NA'.

      */
      $placementHomeAwareDTO: PlacementHomeEntityAwareDTO()
      $placementHome: PlacementHome(facilityType == 1416 || facilityType == 6914, licensrCd != "CT") from $placementHomeAwareDTO.entity
  then
      $placementHome.licensrCd = "CT";
      update($placementHomeAwareDTO)
end

rule "R-03960-NA"
  dialect "mvel"
  agenda-group "placement-home-data-processing-agenda"
  when
      /*
      Code: R - 00195

      Fields: fraLicenser

      Rule Txt

      On add, if the placement facility type is Resource Family Home or Foster Family Home, default Licensed By to 'County', otherwise use 'Not Applicable'.

      Logic

      If PLACEMENT_HOME.Placement_Facility_Type = 'Resource Family Home' or 'Foster Family Home', set .Licenser_Code = 'CT', else set .Licenser_Code = 'NA'.

      */
      $placementHomeAwareDTO: PlacementHomeEntityAwareDTO()
      $placementHome: PlacementHome(!(facilityType == 1416 || facilityType == 6914), licensrCd != "NA") from $placementHomeAwareDTO.entity
  then
      $placementHome.licensrCd = "NA";
      update($placementHomeAwareDTO)
end


