import gov.ca.cwds.rest.exception.IssueDetails
import gov.ca.cwds.data.legacy.cms.entity.PlacementHome
import gov.ca.cwds.cms.data.access.dto.PlacementHomeEntityAwareDTO;

global java.util.Set validationDetailsList


/*
        Code: R - 00168

        Rule Txt
        Payee Name, Payee Street Name, Payee City Name and, if a domestic address is
        entered then also the Payee State Code, must be specified when adding or updating
        Placement Home Payee Details

        Logic
        After performing the process Add Home Payee Details, the PLACEMENT_HOME.Payee_First_Name,
        .Payee_Last_Name, .Payee_Street_Name and .Payee_City_Name must not be null.
        If .Payee_Zip_Number or .Payee_Zip_Suffix_Number is not null
        then .Payee_State_Code_Type must not be null.
*/



/**
* After performing the process Add Home Payee Details, the PLACEMENT_HOME.Payee_First_Name,
* .Payee_Last_Name, .Payee_Street_Name and .Payee_City_Name must not be null.
*/
rule "R-00168_1"
  dialect "mvel"
  agenda-group "placement-home-agenda"
  when
      $placementHomeAwareDTO: PlacementHomeEntityAwareDTO()
      PlacementHome(pyeFstnm != null
        || pyeMidnm != null
        || pyeLstnm != null
        || pstreetNm != null
        || pstreetNo != null
        || pCityNm != null
        || pZipNo != null
        || pyZipSfx != null
        || pyeTelNo != null
        || pyeExtNo != null
        || payeeStateCode != null,

        pyeFstnm == null
        || pyeLstnm == null
        || pstreetNm == null
        || pCityNm == null

        ) from  $placementHomeAwareDTO.entity
  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-00168");
      details.setUserMessage("PLACEMENT_HOME.Payee_First_Name, .Payee_Last_Name, .Payee_Street_Name and .Payee_City_Name must not be null.");
      details.setTechnicalMessage("PLACEMENT_HOME.Payee_First_Name, .Payee_Last_Name, .Payee_Street_Name and .Payee_City_Name must not be null.");
      validationDetailsList.add(details);
end

/**
* If .Payee_Zip_Number or .Payee_Zip_Suffix_Number is not null then .Payee_State_Code_Type must not be null.
*/
rule "R-00168_2"
  dialect "mvel"
  agenda-group "placement-home-agenda"
  when
      $placementHomeAwareDTO: PlacementHomeEntityAwareDTO()
      PlacementHome(pZipNo != null || pyZipSfx != null, payeeStateCode == null) from  $placementHomeAwareDTO.entity
  then
      IssueDetails details = new IssueDetails();
      details.setCode("R-00168");
      details.setUserMessage("PLACEMENT_HOME.Payee_State_Code_Type must not be null.");
      details.setTechnicalMessage("If .Payee_Zip_Number or .Payee_Zip_Suffix_Number is not null then .Payee_State_Code_Type must not be null.");
      validationDetailsList.add(details);
end
